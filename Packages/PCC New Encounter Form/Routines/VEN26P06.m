VEN26P06 ;GDIT/HS/BEE-PCC+ (Well Child) Version 2.6 Patch 6 Post Install - Register Components ; 08 May 2012  12:00 PM
 ;;2.6;PCC+;**6**;NOV 16, 2007;Build 18
 ;
ENV ;EP - Environmental Checking Routine
 ;
 N VERSION,EXEC,BMWDT,BI,DIK,PIEN,TEXT
 ;
 ;Check for PCC+*2.6*5
 I '$$INSTALLD("PCC+*2.6*5") D BMES^XPDUTL("Version 2.6 Patch 5 of PCC+ is required!") S XPDQUIT=2 Q
 ;
 Q
 ;
EN ;EP - Register the Well Child objects into the VUECENTRIC OBJECT REGISTRY
 ;
 ; UPDATE THE VUECENTRIC REGISTERED OBJECTS FILE
 W !,"Registering the Vucentric Objects..."
 F BI=1:1 S TEXT=$P($T(OBJ+BI),";",3,99) Q:($P(TEXT,";")="END")  D
 .W !,$P(TEXT,";")
 . ;
 . NEW DIC,X,Y,OBJUPD,ERROR,WP8,WP9,WP10,CHKSM
 . ;
 . ;PROGID (#.01)
 . S DIC="^CIAVOBJ(19930.2,",DIC(0)="LOX",X=$P(TEXT,";")
 . D ^DIC I +Y<0 Q
 . S OIEN=+Y
 . ;
 . ;NAME (#1)
 . S OBJUPD(19930.2,OIEN_",",1)=$P(TEXT,";",2)
 . ;
 . ;VERSION (#2)
 . S OBJUPD(19930.2,OIEN_",",2)=$P(TEXT,";",3)
 . ;
 . ;SOURCE (#3)
 . S OBJUPD(19930.2,OIEN_",",3)=$P(TEXT,";",4)
 . ;
 . ;SERIALIZABLE (#8)
 . S WP8(1)=$P(TEXT,";",5)
 . D WP^DIE(19930.2,OIEN_",",8,"","WP8")
 . ;
 . ;INITIALIZATION (#9)
 . S WP9(1)=$P(TEXT,";",6)
 . D WP^DIE(19930.2,OIEN_",",9,"","WP9")
 . ;
 . ;REQUIRED (#10)
 . S WP10(1)=$P(TEXT,";",7)
 . D WP^DIE(19930.2,OIEN_",",10,"","WP10")
 . ;
 . ;PROPEDIT (#11)
 . S OBJUPD(19930.2,OIEN_",",11)=$P(TEXT,";",8)
 . ;
 . ;MULTIPLE (#12)
 . S OBJUPD(19930.2,OIEN_",",12)=$P(TEXT,";",9)
 . ;
 . ;DISABLED (#13)
 . S OBJUPD(19930.2,OIEN_",",13)=$P(TEXT,";",10)
 . ;
 . ;ALLKEYS (#14)
 . S OBJUPD(19930.2,OIEN_",",14)=$P(TEXT,";",11)
 . ;
 . ;HIDDEN (#15)
 . S OBJUPD(19930.2,OIEN_",",15)=$P(TEXT,";",12)
 . ;
 . ;SIDEBYSIDE (#16)
 . S OBJUPD(19930.2,OIEN_",",16)=$P(TEXT,";",13)
 . ;
 . ;SERVICE (#17)
 . S OBJUPD(19930.2,OIEN_",",17)=$P(TEXT,";",14)
 . ;
 . ;REGRESS (#18)
 . S OBJUPD(19930.2,OIEN_",",18)=$P(TEXT,";",15)
 . ;
 . ;NOREGISTER (#19)
 . S OBJUPD(19930.2,OIEN_",",19)=$P(TEXT,";",16)
 . ;
 . ;DOTNET (#22)
 . S OBJUPD(19930.2,OIEN_",",22)=$P(TEXT,";",17)
 . ;
 . ;ALIAS (#23)
 . S OBJUPD(19930.2,OIEN_",",23)=$P(TEXT,";",18)
 . ;
 . ;TECHNICAL DESCRIPTION (#98)
 . S OBJUPD(19930.2,OIEN_",",98)=$P(TEXT,";",19)
 . ;
 . ;DESCRIPTION (#99)
 . S OBJUPD(19930.2,OIEN_",",99)=$P(TEXT,";",20)
 . ;
 . ;CLSID (#.5)
 . S OBJUPD(19930.2,OIEN_",",.5)=$P(TEXT,";",21)
 . ;
 . ;HEIGHT (#4)
 . S OBJUPD(19930.2,OIEN_",",4)=$P(TEXT,";",22)
 . ;
 . ;WIDTH (#5)
 . S OBJUPD(19930.2,OIEN_",",5)=$P(TEXT,";",23)
 . ;
 . ;MD5 CHECKSUM
 . S CHKSM=""
 . I $P(TEXT,";")["IHS.WCM.EHR.ASQ.ASQCOMPONENT" S CHKSM="432D29531C9C95860BA069D8DE2B1038"
 . I $P(TEXT,";")["IHS.WCM.EHR.GROWTHCHARTS.CHARTCOMPONENT" S CHKSM="9BE2E9AFBF36ACB8336A6F85CCACEF29"
 . I $P(TEXT,";")["IHS.WCM.EHR.PATIENTED.PATIENTEDCOMPONENT" S CHKSM="C068864B5F386137BA98A8217598401D"
 . I $P(TEXT,";")["IHS.WCM.EHR.REMINDERS.REMINDERSCOMPONENT" S CHKSM="A3629C374A55D9F9A90D8DEBFD4D5866"
 . I CHKSM]"" S OBJUPD(19930.2,OIEN_",",7)=CHKSM
 . ;
 . ;Update entry
 .D FILE^DIE("","OBJUPD","ERROR")
 ;
 ;Locate ASQ entry
 S DIC(0)="MU",X="IHS.WCM.EHR.ASQ.ASQCOMPONENT",DIC="^CIAVOBJ(19930.2,"
 D ^DIC I +Y<0 G XREG
 S PIEN=+Y
 ;
 ;Enter USER values for ASQ Entry
 NEW RMV,UIEN
 F BI=1:1 S TEXT=$P($T(ASQUSE+BI),";",3,99) Q:($P(TEXT,";")="END")  D
 . ;
 . NEW DIC,X,Y,DA,OIEN,REMOVE
 . ;
 . ;Locate IEN for this entry
 . S DIC="^CIAVOBJ(19930.2,",X=$P(TEXT,";"),DIC(0)="OX"
 . D ^DIC I +Y<0 W !!,"<MISSING VUECENTRIC OBJECT: ",X,">" Q
 . S OIEN=+Y
 . ;
 . S DA(1)=PIEN,DIC="^CIAVOBJ(19930.2,"_DA(1)_",9,",DIC(0)="LOX",X=$P(TEXT,";")
 . D ^DIC
 . ;
 . ;Get v8.3 User entries
 . S REMOVE=$P(TEXT,";",2) Q:REMOVE=""
 . S RMV(REMOVE)=""
 ;
 ;Remove v8.3 User entries for ASQ Entry
 I $D(RMV) S UIEN=0 F  S UIEN=$O(^CIAVOBJ(19930.2,PIEN,9,UIEN)) Q:'UIEN  D
 . NEW USEN,DA,IENS
 . S DA(1)=PIEN,DA=UIEN,IENS=$$IENS^DILF(.DA)
 . S USEN=$$GET1^DIQ(19930.221,IENS,".01","E") Q:USEN=""
 . I '$D(RMV(USEN)) Q
 . S DIK="^CIAVOBJ(19930.2,"_DA(1)_",9,",DA=UIEN D ^DIK
 K RMV,UIEN
 ;
 ;Locate GrowthCharts entry
 S DIC(0)="MU",X="IHS.WCM.EHR.GROWTHCHARTS.CHART",DIC="^CIAVOBJ(19930.2,"
 D ^DIC I +Y<0 G XREG
 S PIEN=+Y
 ;
 ;Enter USER values for Pick List Entry
 F BI=1:1 S TEXT=$P($T(GCUSE+BI),";",3,99) Q:($P(TEXT,";")="END")  D
 . ;
 . NEW DIC,X,Y,DA,OIEN
 . ;
 . ;Locate IEN for this entry
 . S DIC="^CIAVOBJ(19930.2,",X=$P(TEXT,";"),DIC(0)="OX"
 . D ^DIC I +Y<0 W !!,"<MISSING VUECENTRIC OBJECT: ",X,">" Q
 . S OIEN=+Y
 . ;
 . S DA(1)=PIEN,DIC="^CIAVOBJ(19930.2,"_DA(1)_",9,",DIC(0)="LOX",X=$P(TEXT,";")
 . D ^DIC
 ;
 ;Locate PATIENTED entry
 S DIC(0)="MU",X="IHS.WCM.EHR.PATIENTED.PATIENTE",DIC="^CIAVOBJ(19930.2,"
 D ^DIC I +Y<0 G XREG
 S PIEN=+Y
 ;
 ;Enter USER values for Pick List Entry
 NEW RMV,UIEN
 F BI=1:1 S TEXT=$P($T(PIUSE+BI),";",3,99) Q:($P(TEXT,";")="END")  D
 . ;
 . NEW DIC,X,Y,DA,OIEN,REMOVE
 . ;
 . ;Locate IEN for this entry
 . S DIC="^CIAVOBJ(19930.2,",X=$P(TEXT,";"),DIC(0)="OX"
 . D ^DIC I +Y<0 W !!,"<MISSING VUECENTRIC OBJECT: ",X,">" Q
 . S OIEN=+Y
 . ;
 . S DA(1)=PIEN,DIC="^CIAVOBJ(19930.2,"_DA(1)_",9,",DIC(0)="LOX",X=$P(TEXT,";")
 . D ^DIC
 . ;
 . ;Get v8.3 User entries
 . S REMOVE=$P(TEXT,";",2) Q:REMOVE=""
 . S RMV(REMOVE)=""
 ;
 ;Remove v8.3 User entries for Pick List Entry
 I $D(RMV) S UIEN=0 F  S UIEN=$O(^CIAVOBJ(19930.2,PIEN,9,UIEN)) Q:'UIEN  D
 . NEW USEN,DA,IENS
 . S DA(1)=PIEN,DA=UIEN,IENS=$$IENS^DILF(.DA)
 . S USEN=$$GET1^DIQ(19930.221,IENS,".01","E") Q:USEN=""
 . I '$D(RMV(USEN)) Q
 . S DIK="^CIAVOBJ(19930.2,"_DA(1)_",9,",DA=UIEN D ^DIK
 K RMV,UIEN
 ;
 ;Locate REMINDERS entry
 S DIC(0)="MU",X="IHS.WCM.EHR.REMINDERS.REMINDER",DIC="^CIAVOBJ(19930.2,"
 D ^DIC I +Y<0 G XREG
 S PIEN=+Y
 ;
 ;Enter USER values for Pick List Entry
 NEW RMV,UIEN
 F BI=1:1 S TEXT=$P($T(REUSE+BI),";",3,99) Q:($P(TEXT,";")="END")  D
 . ;
 . NEW DIC,X,Y,DA,OIEN,REMOVE
 . ;
 . ;Locate IEN for this entry
 . S DIC="^CIAVOBJ(19930.2,",X=$P(TEXT,";"),DIC(0)="OX"
 . D ^DIC I +Y<0 W !!,"<MISSING VUECENTRIC OBJECT: ",X,">" Q
 . S OIEN=+Y
 . ;
 . S DA(1)=PIEN,DIC="^CIAVOBJ(19930.2,"_DA(1)_",9,",DIC(0)="LOX",X=$P(TEXT,";")
 . D ^DIC
 . ;
 . ;Get v8.3 User entries
 . S REMOVE=$P(TEXT,";",2) Q:REMOVE=""
 . S RMV(REMOVE)=""
 ;
 ;Remove v8.3 User entries for Reminders Entry
 I $D(RMV) S UIEN=0 F  S UIEN=$O(^CIAVOBJ(19930.2,PIEN,9,UIEN)) Q:'UIEN  D
 . NEW USEN,DA,IENS
 . S DA(1)=PIEN,DA=UIEN,IENS=$$IENS^DILF(.DA)
 . S USEN=$$GET1^DIQ(19930.221,IENS,".01","E") Q:USEN=""
 . I '$D(RMV(USEN)) Q
 . S DIK="^CIAVOBJ(19930.2,"_DA(1)_",9,",DA=UIEN D ^DIK
 K RMV,UIEN
 ;
XREG Q
 ;
 ;;File 19930.2 Field listing
 ;;PROGID;NAME;VRSN;SRC;SER;INI;REQ;PROP;MULT;DIS;ALLK;HIDD;SBYS;SERV;REG;NORG;DOTN;ALIA;TDES;DES;CLSID;HEIGHT;WIDTH
 ;;.01;1;2;3;8;9;10;11;12;13;14;15;16;17;18;19;22;23;98;99.5;4;5
OBJ ;;
 ;;FILE:INFRAGISTICS2.SHARED.V10.3.DLL;Shared.v10.3;10.3.20103.1000;Infragistics2.Shared.v10.3.dll;;;;0;0;0;0;1;0;0;0;;0;;;;;;
 ;;FILE:INFRAGISTICS2.WIN.MISC.V10.3.DLL;Misc.v10.3;10.3.20103.1000;Infragistics2.Win.Misc.v10.3.dll;;;;0;0;0;0;1;0;0;0;;0;;;;;;
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINEDITORS.V10.3.DLL;UltraWinEditors.v10.3;10.3.20103.1000;Infragistics2.Win.UltraWinEditors.v10.3.dll;;;;0;0;0;0;1;0;0;0;;0;;;;;;
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V10.3.DLL;UltraWinGrid.v10.3;10.3.20103.1000;Infragistics2.Win.UltraWinGrid.v10.3.dll;;;;0;0;0;0;1;0;0;0;;0;;;;;;
 ;;FILE:INFRAGISTICS2.WIN.V10.3.DLL;Win.v10.3;10.3.20103.1000;Infragistics2.Win.v10.3.dll;;;;0;0;0;0;1;0;0;0;;0;;;;;;
 ;;IHS.WCM.EHR.ASQ.ASQCOMPONENT;Well Child ASQ;1.5.0.1;IHS.WCM.EHR.ASQ.dll;;;IHS.WCM.EHR.ASQ.chm;0;1;0;0;0;0;0;0;;1;;;;{E75D9C45-8BFD-4FE0-A322-941DF2CAC1D9};300;600;
 ;;IHS.WCM.EHR.GROWTHCHARTS.CHARTCOMPONENT;Well Child Pediatric Growth Charts;1.5.0.0;IHS.WCM.EHR.GrowthCharts.dll;PrintHRN=BOOL;;IHS.WCM.EHR.GrowthCharts.chm;0;1;0;0;0;0;0;0;;1;;;;{916E390F-CBE4-4FD0-95E0-5C26AA191906};640;480;
 ;;IHS.WCM.EHR.PATIENTED.PATIENTEDCOMPONENT;Well Child Patient Education;1.5.0.0;IHS.WCM.EHR.PATIENTED.dll;;;IHS.WCM.EHR.PatientEd.chm;0;1;0;0;0;0;0;0;;1;;;;{4F37CE40-077C-4F5B-94BC-180952737BF4};100;400;
 ;;IHS.WCM.EHR.REMINDERS.REMINDERSCOMPONENT;Well Child Reminders List;1.5.0.0;IHS.WCM.EHR.REMINDERS.dll;;;IHS.WCM.EHR.Reminders.chm;0;1;0;0;0;0;0;0;;1;;;;{8D15368C-CE3D-49FE-AFAD-BEF6C3E8D83B};480;640;
 ;;END;
 ;;
ASQUSE ;;
 ;;FILE:INFRAGISTICS2.SHARED.V10.3.DLL;FILE:INFRAGISTICS2.SHARED.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINEDITORS.V10.3.DLL;FILE:INFRAGISTICS2.WIN.ULTRAWINEDITORS.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V10.3.DLL;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.V10.3.DLL;FILE:INFRAGISTICS2.WIN.V8.3.DLL
 ;;FILE:ITEXTSHARP.3.1.6.DLL
 ;;END;
 ;;
GCUSE ;;
 ;;FILE:ITEXTSHARP.3.1.6.DLL
 ;;END;
 ;;
PIUSE ;;
 ;;FILE:INFRAGISTICS2.SHARED.V10.3.DLL;FILE:INFRAGISTICS2.SHARED.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V10.3.DLL;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.V10.3.DLL;FILE:INFRAGISTICS2.WIN.V8.3.DLL
 ;;END;
 ;;
REUSE ;;
 ;;FILE:INFRAGISTICS2.SHARED.V10.3.DLL;FILE:INFRAGISTICS2.SHARED.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V10.3.DLL;FILE:INFRAGISTICS2.WIN.ULTRAWINGRID.V8.3.DLL
 ;;FILE:INFRAGISTICS2.WIN.V10.3.DLL;FILE:INFRAGISTICS2.WIN.V8.3.DLL
 ;;END;
 ;;
INSTALLD(VENSTAL) ;EP - Determine if patch VENSTAL was installed, where
 ;VENSTAL is the name of the INSTALL.  E.g "VEN*2.6*5".
 ;
 I $D(^XPD(9.6,"B",VENSTAL)) Q 1
 Q 0
