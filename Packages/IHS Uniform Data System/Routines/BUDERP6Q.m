BUDERP6Q ;IHS/CMI/LAB - UDS T6B;
 ;;12.0;IHS/RPMS UNIFORM DATA SYSTEM;;NOV 22, 2017;Build 75
 ;
L ;EP  - NEW HIV
 NEW BUDGOT,BUDDOA
 S BUDGOT=""
 S BUDDOB=$P(^DPT(DFN,0),U,3)
 Q:BUDMEDV<1
 S BUDX28TH=$E(BUDDOB,1,3)+18_$E(BUDDOB,4,7)
 K BUDG
 S Y="BUDG("
 S X=DFN_"^FIRST DX [BUD HIV DXS" S E=$$START1^APCLDF(X,Y)
 I '$D(BUDG(1)) Q
 S BUDHIVD=$P(BUDG(1),U,1) ;FIRST DX IN RPMS
 S BUDHIVDX=$P(BUDG(1),U,2)
 S D=($E(BUDBD,1,3)-1)_"1001"
 S E=$E(BUDED,1,3)_"0930"
 I BUDHIVD<D Q
 I BUDHIVD>E Q
 S BUDDOA=""
 ;now check problem list date of onset
 S T=$O(^BUDETSSC("B","T6B HIV HIV CODES",0))
 S (X,G)=0,Z="" F  S X=$O(^AUPNPROB("AC",DFN,X)) Q:X'=+X  D
 .Q:$P(^AUPNPROB(X,0),U,12)="D"
 .Q:$P(^AUPNPROB(X,0),U,13)=""
 .S Y=$P(^AUPNPROB(X,0),U)
 .;)
 .Q:'$D(^BUDETSSC("AD",Y,T))
 .S BUDDOA=$P(^AUPNPROB(X,0),U,13)
 .I $P(^AUPNPROB(X,0),U,13)<BUDHIVD S G=1
 .Q
 I G Q
 S BUDHIVF=$$FU(DFN,$$FMADD^XLFDT(BUDHIVD,1))
 I BUDHIVF S BUDSECTL("HIV")=$G(BUDSECTL("HIV"))+1
S ;put the rest in demoninator
 S BUDSECTL("PTS")=$G(BUDSECTL("PTS"))+1 D
 .I $G(BUDHIV2L) D
 ..I BUDHIVF="" S ^XTMP("BUDERP6B",BUDJ,BUDH,"HIV2",BUDAGE,$P(^DPT(DFN,0),U),BUDCCOM,DFN)="First HIV: "_BUDHIVDX_": "_$$DATE^BUDEUTL1(BUDHIVD)_"|Follow-up: "_$P(BUDHIVF,U,2)_" "_$$DATE^BUDEUTL1($P(BUDHIVF,U,1))_"|"_$$DATE^BUDEUTL1(BUDDOA)
 .I $G(BUDHIV1L) D
 ..I BUDHIVF]"" S ^XTMP("BUDERP6B",BUDJ,BUDH,"HIV1",BUDAGE,$P(^DPT(DFN,0),U),BUDCCOM,DFN)="First HIV: "_BUDHIVDX_": "_$$DATE^BUDEUTL1(BUDHIVD)_"|Follow-up: "_$P(BUDHIVF,U,2)_" "_$$DATE^BUDEUTL1($P(BUDHIVF,U,1))_"|"_$$DATE^BUDEUTL1(BUDDOA)
 Q
FU(P,BD) ;WAS THERE AN HIV VISIT WITHIN 90 DAYS OF BD
 NEW BUDVS
 S ED=$$FMADD^XLFDT(BD,90)
 D ALLV^APCLAPIU(P,BD,ED,"BUDVS")
 I '$D(BUDVS) Q ""
 NEW X,Y,V,G,D,C,T,L1,L2,L3
 S G=""
 S X=0 F  S X=$O(BUDVS(X)) Q:X'=+X!(G)  D
 .S V=$P(BUDVS(X),U,5)
 .S C=$$CLINIC^APCLV(V,"C") I C=59 S G=$P(BUDVS(X),U,1)_U_"Clinic: 59" Q
 .S D=$$PRIMPOV^APCLV(V,"I")
 .I $$ICD^ATXCHK(D,$O(^ATXAX("B","BUD HIV DXS",0)),9) S G=$P(BUDVS(X),U,1)_U_"DX: "_$P(^ICD9(D,0),U,1) Q
 .S C=0 F  S C=$O(^AUPNVCPT("AD",V,C)) Q:C'=+C!(G)  D
 ..S T=$$VALI^XBDIQ1(9000010.18,C,.01)
 ..I $$ICD^ATXCHK(T,$O(^ATXAX("B","BUD HIV FU CPTS",0)),1) S G=$P(BUDVS(X),U,1)_U_"CPT: "_$P(^ICPT(T,0),U,1) Q
 .S L1=$O(^ATXLAB("B","BGP HIV TEST TAX",0))
 .S L2=$O(^ATXLAB("B","BGP CD4 TAX",0))
 .S L3=$O(^ATXLAB("B","BGP HIV VIRAL LOAD TAX",0))
 .S C=0 F  S C=$O(^AUPNVLAB("AD",V,C)) Q:C'=+C!(G)  D
 ..S T=$$VALI^XBDIQ1(9000010.09,C,.01)
 ..I L1,$D(^ATXLAB(L1,21,"B",T)) S G=$P(BUDVS(X),U,1)_U_"LAB: "_$P(^LAB(60,T,0),U,1) Q
 ..I L2,$D(^ATXLAB(L2,21,"B",T)) S G=$P(BUDVS(X),U,1)_U_"LAB: "_$P(^LAB(60,T,0),U,1) Q
 ..I L3,$D(^ATXLAB(L3,21,"B",T)) S G=$P(BUDVS(X),U,1)_U_"LAB: "_$P(^LAB(60,T,0),U,1) Q
 Q G
M ;EP
 NEW BUDGOT
 S BUDGOT=""
 S BUDDOB=$P(^DPT(DFN,0),U,3)
 Q:BUDMEDV<1
 S BUDX12RB=($E(BUDBD,1,3)-12)_"1231"
 Q:BUDDOB>BUDX12RB
 S BUDP12BD=($E(BUDDOB,1,3)+12)_$E(BUDDOB,4,7)
 K BUDG
 ;DENOMINATOR EXCLUSION, ANY ONE WITH ACTIVE PL ENTRY OF THE DX OR SNOMED
 ;GET DATE OF FIRST SCREEN IN REPORT PERIOD
 I $$HASDEPPL(DFN,"T6B DEP DEP/BIPOLAR CODES",$$VD^APCLV(BUDFRSTV)) Q  ;pcc or bh problem list
 ;Q:$$HASDEPOV(DFN,$$DOB^AUPNPAT(DFN),$$FMADD^XLFDT($$VD^APCLV(BUDFRSTV),-1))  ;PCC DX DURING REPORT PERIOD
 S BUDFDEP=$$FDEPSCR^BUDERP6M(DFN,BUDBD,BUDED)
 ;if screen, was there a diagnosis before the SCREEN AND DURING REPORT PERIOD? if so, quit
 I BUDFDEP Q:$$HASDEPOV(DFN,BUDBD,BUDFDEP)
 I 'BUDFDEP Q:$$HASDEPOV(DFN,BUDBD,BUDED)  ;if no screen quit if any dx during report period
 ;
 ;DO THEY HAVE A SCREEN?
 ;CHECK exam, phq2, phq9, phqt for a positive and if no positive get the first negative
 S (BUDSCR,BUDPLAN)=""
 S BUDSCR=$$DEPRES(DFN,BUDBD,BUDED)  ;MOST RECENT WITH A RESULT
 I BUDSCR]"" Q:$$HASDEPOV(DFN,BUDBD,$P(BUDSCR,U,1))  ;dx prior to screen and during report period
 I BUDSCR="",$$REFDS(DFN,BUDBD,BUDED) Q  ; NO SCREEN BUT HAS REFUSAL
 I BUDSCR]"" G R
 ;now check for any without a result and assume negative (per Duane)
 ;S BUDSCR=$$DEPNORES(DFN,BUDBD,BUDED)  ;NO CPTS PER MEGAN
R ;
 S BUDSECTM("PTS")=$G(BUDSECTM("PTS"))+1  ;DENOMINATOR
 S BUDN=0
 I BUDSCR="" G M1
 I $P(BUDSCR,U,3)["NEG" S BUDSECTM("DEP")=$G(BUDSECTM("DEP"))+1 S BUDN=1 G M1
 ;CHECK FOR PLAN
 S BUDPLAN=$$PLAN(DFN,$P(BUDSCR,U,1),$P(BUDSCR,U,1))  ;PLAN BETWEEN DATE OF SCREEN AND END OF REPORT PERIOD
 I BUDPLAN]"" S BUDSECTM("DEP")=$G(BUDSECTM("DEP"))+1,BUDN=1
M1 ;LISTS
 D
 .I $G(BUDDEP2L),'BUDN D
 ..S ^XTMP("BUDERP6B",BUDJ,BUDH,"DEP2",BUDAGE,$P(^DPT(DFN,0),U),BUDCCOM,DFN)=BUDSCR_"|"_BUDPLAN
 .I $G(BUDDEP1L),BUDN D
 ..S ^XTMP("BUDERP6B",BUDJ,BUDH,"DEP1",BUDAGE,$P(^DPT(DFN,0),U),BUDCCOM,DFN)=BUDSCR_"|"_BUDPLAN
 Q
REFDS(P,BDATE,EDATE) ;
 I $$REFRU^BUDEUTL1(P,9999999.15,$O(^AUTTEXAM("C",36,0)),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,81,$P($$CPT^ICPTCOD("3725F"),U,1),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,81,$P($$CPT^ICPTCOD("1220F"),U,1),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,81,$P($$CPT^ICPTCOD("G0444"),U,1),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9999999.07,$O(^AUTTMSR("B","PHQ2",0)),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9999999.07,$O(^AUTTMSR("B","PHQ9",0)),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9999999.07,$O(^AUTTMSR("B","PHQT",0)),BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,105480006,BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,183944003,BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,183945002,BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,413310006,BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,413311005,BDATE,EDATE) Q 1
 I $$REFRU^BUDEUTL1(P,9002318.4,413312003,BDATE,EDATE) Q 1
 Q ""
PLAN(P,BDATE,EDATE) ;
 ;CHECK MEDS, CPTS, SNOMEDS
 NEW BUDVS,BUDDEPS,BUDV,BUDX,BUDA,BUDD,BUDP,BUDY,X,T,Y,A,B,J,D,R,K,BUDRX,BUDRXRF,BUDXRX,BUDNORM
 ;BGPDEPS=DATE SCREEN^SCREEN ITEM^SCREEN RESULT^FU PLAN ITEM
 S BUDVS="BUDVS"
 D ALLV^APCLAPIU(P,BDATE,EDATE,.BUDVS)
 S BUDX=0,BUDD=0,BUDDEPS=""
 S T=$O(^BUDETSSC("B","T6B DEP DEPRESSION PLAN CODES",0))
 S J=$O(^ATXAX("B","BGP HEDIS ANTIDEPRESSANT MEDS",0))
 S K=$O(^ATXAX("B","BGP HEDIS ANTIDEPRESSANT NDC",0))
 F  S BUDX=$O(BUDVS(BUDX)) Q:BUDX'=+BUDX!(BUDDEPS]"")  D
 .;DX/snomed
 .S BUDV=$P(BUDVS(BUDX),U,5)
 .S BUDY=0  F  S BUDY=$O(^AUPNVPOV("AD",BUDV,BUDY)) Q:BUDY'=+BUDY!(BUDDEPS]"")  D
 ..Q:'$D(^AUPNVPOV(BUDY,0))
 ..;NOW CHECK SNOMED
 ..S A=$P($G(^AUPNVPOV(BUDY,11)),U,1)
 ..Q:A=""
 ..I $D(^BUDETSSC("AS",A,T)) S BUDDEPS=$$VD^APCLV(BUDV)_U_"SNOMED: "_A Q
 .;CPT
 .S BUDY=0  F  S BUDY=$O(^AUPNVCPT("AD",BUDV,BUDY)) Q:BUDY'=+BUDY!(BUDDEPS]"")  D
 ..Q:'$D(^AUPNVCPT(BUDY,0))
 ..S A=$$VAL^XBDIQ1(9000010.18,BUDY,.01)
 ..Q:'$D(^BUDETSSC("AC",A,T))
 ..S BUDDEPS=$$VD^APCLV(BUDV)_U_"CPT: "_A
 .;NOW CHECK MEDS
 .S BUDY=0 F  S BUDY=$O(^AUPNVMED("AD",BUDV,BUDY)) Q:BUDY'=+BUDY!(BUDDEPS]"")  D
 ..Q:'$D(^AUPNVMED(BUDY,0))
 ..S D=$P(^AUPNVMED(BUDY,0),U,1)
 ..Q:D=""
 ..Q:'$D(^PSDRUG(D,0))
 ..I $D(^ATXAX(J,21,"B",D)) S BUDDEPS=$$VD^APCLV(BUDV)_U_"RX: "_$$VAL^XBDIQ1(9000010.14,BUDY,.01) Q
 ..;check NDC
 ..I $$NDC(D,K) S BUDDEPS=$$VD^APCLV(BUDV)_U_"RX: "_$$VAL^XBDIQ1(9000010.14,BUDY,.01) Q
 ..;check rxnorm?
 ..;GET PRESCRIPTION
 ..S BUDRXRF=""
 ..S BUDRX=$O(^PSRX("APCC",BUDY,0)) I 'BUDRX Q  ;NO 52 ENTRY
 ..I BUDRX S BUDRXRF=$O(^PSRX("APCC",BUDY,BUDRX,"")) S:BUDRXRF="" BUDRXRF=0
 ..S BUDNORM=""
 ..I BUDRXRF S BUDNORM=$P($G(^PSRX(BUDRX,1,BUDRXRF,9999999)),U,19)
 ..I BUDNORM="" S BUDNORM=$P($G(^PSRX(BUDRX,999999921)),U,7)
 ..I BUDNORM]"",$D(^BUDETSSC(T,19,"B",BUDNORM)) S BUDDEPS=$$VD^APCLV(BUDV)_U_"RX NORM: "_$$VAL^XBDIQ1(9000010.14,BUDY,.01) Q
 I BUDDEPS]"" Q BUDDEPS
 ;
 S D=0,E=9999999-BDATE,D=9999999-EDATE-1_".99" F  S D=$O(^AMHREC("AE",P,D)) Q:D'=+D!($P(D,".")>E)!(BUDDEPS]"")  S V=0 F  S V=$O(^AMHREC("AE",P,D,V)) Q:V'=+V  D
 .S X=0 F  S X=$O(^AMHRPROC("AD",V,X)) Q:X'=+X  S BUDP=$$VAL^XBDIQ1(9002011.04,X,.01) D
 ..Q:BUDP=""
 ..Q:'$D(^BUDETSSC("AC",BUDP,T))
 ..S BUDDEPS=9999999-$P(D,".")_U_"BH CPT: "_$$GET1^DIQ(9002011.04,X,.01)
 I BUDDEPS]"" Q BUDDEPS
EHRO ;EPRES
 ;EHR OUTSIDE
 S C=$$PRES^BUDERP6W(P,J,BDATE,EDATE,K)
 I C]"" Q 1_U_$P(C,U,1)_" on "_$$FMTE^XLFDT($P(C,U,3))
 Q ""
NDC(A,B) ;
 ;a is drug ien
 ;b is taxonomy ien
 NEW BUDNDC
 S BUDNDC=$P($G(^PSDRUG(A,2)),U,4)
 I BUDNDC]"",B,$D(^ATXAX(B,21,"B",BUDNDC)) Q 1
 Q 0
DEPRES(P,BD,ED) ;
 ;DID PT HAVE AN EXAM 36 DURING REPORT PERIOD
 NEW %,E,D,V,X,G,BUDD
 NEW BUDG,BUDR,BUDALL
 S BUDD=0
 K BUDG S %=P_"^ALL EXAM 36;DURING "_BD_"-"_ED,E=$$START1^APCLDF(%,"BUDG(")
 S E=0 F  S E=$O(BUDG(E)) Q:E'=+E  D
 .I $P(BUDG(E),U,2)="?" Q  ;no result
 .S BUDR=$S($P(BUDG(E),U,2)="PO":"POS",$P(BUDG(E),U,2)="RF":"POS",1:"NEG")
 .S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"PCC EX 36"_U_BUDR_" "_$P(BUDG(E),U,2)
 ;phq2
 K BUDG S %=P_"^ALL MEAS PHQ2;DURING "_BD_"-"_ED,E=$$START1^APCLDF(%,"BUDG(")
 S E=0 F  S E=$O(BUDG(E)) Q:E'=+E  D
 .S BUDR=$$MRES($P(BUDG(E),U,3),$P(BUDG(E),U,2))
 .S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"PCC PHQ2"_U_BUDR
 K BUDG S %=P_"^ALL MEAS PHQ9;DURING "_BD_"-"_ED,E=$$START1^APCLDF(%,"BUDG(")
 S E=0 F  S E=$O(BUDG(E)) Q:E'=+E  D
 .S BUDR=$$MRES($P(BUDG(E),U,3),$P(BUDG(E),U,2))
 .S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"PCC PHQ9"_U_BUDR
 K BUDG S %=P_"^ALL MEAS PHQT;DURING "_BD_"-"_ED,E=$$START1^APCLDF(%,"BUDG(")
 S E=0 F  S E=$O(BUDG(E)) Q:E'=+E  D
 .S BUDR=$$MRES($P(BUDG(E),U,3),$P(BUDG(E),U,2))
 .S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"PCC PHQT"_U_BUDR
 ;SNOMED
 ;ALL POVS
 K BUDG S %=P_"^ALL DX;DURING "_BD_"-"_ED,E=$$START1^APCLDF(%,"BUDG(")
 S E=0 F  S E=$O(BUDG(E)) Q:E'=+E  D
 .S BUDR=+$P(BUDG(E),U,4)
 .I $P($G(^AUPNVPOV(BUDR,11)),U,1)=428181000124104 D
 ..S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"SNOMED "_"428181000124104"_U_"POS"
 .I $P($G(^AUPNVPOV(BUDR,11)),U,1)=428171000124102 D
 ..S BUDALL(9999999-$P(BUDG(E),U,1))=$P(BUDG(E),U,1)_U_"SNOMED "_"428171000124102"_U_"NEG"
 ;BH EXAM
BHSCR ;
 S D=0,E=9999999-BD,D=9999999-ED-1_".9999"
 F  S D=$O(^AMHREC("AE",P,D)) Q:D'=+D!($P(D,".")>E)  S V=0 F  S V=$O(^AMHREC("AE",P,D,V)) Q:V'=+V  D
 .I $P($G(^AMHREC(V,14)),U,5)="N" S I=9999999-$P(D,".") I '$D(BUDALL($P(D,"."))) S BUDR="NEG",BUDALL($P(D,"."))=I_U_"BH EX"_U_BUDR
 .I $P($G(^AMHREC(V,14)),U,5)="P"!($P($G(^AMHREC(V,14)),U,5)="REF") S I=9999999-$P(D,".") I '$D(BUDALL("POS",I)) S BUDR="POS",BUDALL($P(D,"."))=I_U_"BH EX"_U_BUDR
 .S X=0 F  S X=$O(^AMHRMSR("AD",V,X)) Q:X'=+X  S BUDP=$P($G(^AMHRMSR(X,0)),U) D
 ..Q:'BUDP
 ..S BUDP=$P($G(^AUTTMSR(BUDP,0)),U)
 ..I BUDP="PHQ2"!(BUDP="PHQ9")!(BUDP="PHQT") D
 ...S BUDR=$$MRES(BUDP,$P(^AMHRMSR(X,0),U,4))
 ...I '$D(BUDALL($P(D,"."))) D
 ....S BUDALL($P(D,"."))=(9999999-$P(D,"."))_U_"BH "_BUDP_U_BUDR
 ;I $D(BUDALL("POS")) S X=$O(BUDALL("POS",0)) Q BUDALL("POS",X)
 ;I $D(BUDALL("NEG")) S Y=$O(BUDALL("NEG",0)) Q BUDALL("NEG",Y)
 S X=$O(BUDALL(0)) I 'X Q ""
 Q BUDALL(X)
 ;Q ""
PRIMPOV(V) ;
 NEW Y,Z,P
 S Y=0,Z=""
 I $P(^AUPNVSIT(V,0),U,7)="H" F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y  I $P(^AUPNVPOV(Y,0),U,12)="P" S Z=Y
 I $P(^AUPNVSIT(V,0),U,7)'="H" S Y=$O(^AUPNVPOV("AD",V,0)) I Y S Z=Y
 Q Z
HASDEPOV(P,BDATE,EDATE) ;EP - 
 I '$G(P) Q ""
 NEW T,B,BUDVS,BUDX,BUDV,BUDDX2,BUDDX3,BUDDX4,BUDDX5
 S T=$O(^BUDETSSC("B","T6B DEP DEP/BIPOLAR CODES",0))
 S BUDVS="BUDVS",BUDDX4=""
 D ALLV^APCLAPIU(P,BDATE,EDATE,"BUDVS")
 S BUDX=0 F  S BUDX=$O(BUDVS(BUDX)) Q:BUDX'=+BUDX!(BUDDX4)  D
 .S BUDV=$P(BUDVS(BUDX),U,5)
 .S BUDI=$$PRIMPOV(BUDV)
 .Q:BUDI=""
 .S BUDDX3=$P($G(^AUPNVPOV(BUDI,0)),U)
 .I $D(^BUDETSSC("AD",BUDDX3,T)) S BUDDX4=1_"^"_$P($$ICDDX^ICDEX(BUDDX3),U,2)_"^"_$$VD^APCLV(BUDV)_"^"_BUDDX3 Q  ;FOUND ONE
 .;NOW CHECK SNOMED
 .S A=$P($G(^AUPNVPOV(BUDI,11)),U,1)
 .Q:A=""
 .I $D(^BUDETSSC("AS",A,T)) S BUDDX4=1_"^"_$P($$ICDDX^ICDEX(BUDDX3),U,2)_"^"_$$VD^APCLV(BUDV)_"^"_BUDDX3 Q  ;FOUND ONE
 .Q
 I BUDDX4 Q BUDDX4
 S SD=$$FMADD^XLFDT(BDATE,-1),SD=SD_".9999"
 F  S SD=$O(^AMHREC("AF",P,SD)) Q:SD'=+SD!($P(SD,".")>EDATE)!(BUDDX4)  D
 .S BUDDX2=0 F  S BUDDX2=$O(^AMHREC("AF",P,SD,BUDDX2)) Q:BUDDX2'=+BUDDX2!(BUDDX4)  D
 ..S BUDDX5=0 S BUDDX5=$O(^AMHRPRO("AD",BUDDX2,BUDDX5)) Q:BUDDX5=""!(BUDDX4]"")  D
 ...S BUDDX3=$P($G(^AMHRPRO(BUDDX5,0)),U,1)
 ...Q:BUDDX3=""
 ...S Z=$P($G(^AMHPROB(BUDDX3,0)),U,5)
 ...I Z="" Q
 ...S Y=+$$CODEN^ICDEX(Z,80)
 ...I $D(^BUDETSSC("AD",Y,T)) S BUDDX4=1_"^"_$P($$ICDDX^ICDEX(Y),U,2)_"^"_SD_"^"_BUDDX3_"^"_BUDDX5 Q  ;FOUND ONE
 ...S Z=$P($G(^AMHPROB(BUDDX3,0)),U,17)
 ...I Z="" Q
 ...S Y=+$$CODEN^ICDEX(Z,80)
 ...I $D(^BUDETSSC("AD",Y,T)) S BUDDX4=1_"^"_$P($$ICDDX^ICDEX(Y),U,2)_"^"_SD_"^"_BUDDX3_"^"_BUDDX5 Q  ;FOUND ONE
 Q BUDDX4
HASDEPPL(P,A,BD) ;EP - ACTIVE DEPRESSION ON PL
 I $G(P)="" Q ""
 I $G(A)="" Q ""
 N T,N S T=$O(^BUDETSSC("B",A,0))
 I 'T Q ""
 N X,Y,I,A,D,G S (X,Y)=0,I="" F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X!(I)  I $D(^AUPNPROB(X,0)) D
 .Q:$P(^AUPNPROB(X,0),U,12)="D"
 .Q:$P(^AUPNPROB(X,0),U,12)="I"
 .S A=0,D="",G="" F  S A=$O(^AUPNPROB(X,14,A)) Q:A'=+A!(G)  D
 ..S D=$$VD^APCLV($P(^AUPNPROB(X,14,A,0),U,1))
 ..I D'>BD S G=1
 .I 'G S A=0,D="" F  S A=$O(^AUPNPROB(X,15,A)) Q:A'=+A!(G)  D
 ..S D=$$VD^APCLV($P(^AUPNPROB(X,15,A,0),U,1))
 ..I D'>BD S G=1
 .I 'G I $P(^AUPNPROB(X,0),U,8)'>BD S G=1
 .I 'G I $P(^AUPNPROB(X,0),U,3)'>BD S G=1
 .Q:'G
 .S Y=$P(^AUPNPROB(X,0),U) I $D(^BUDETSSC("AD",Y,T)) S I=1_U_$$VAL^XBDIQ1(9000011,X,.01)_U_$P(^AUPNPROB(X,0),U,3) Q
 .S N=$$VAL^XBDIQ1(9000011,X,80001) I N]"",$D(^BUDETSSC("AS",N,T)) S I=1_U_N_U_$P(^AUPNPROB(X,0),U,3)
 Q I
 ;
DEPNORES(P,BDATE,EDATE) ;EP
 ;
 NEW BUDVS,BUDDEPS,BUDV,BUDX,BUDA,BUDD,BUDP,BUDY,X,T,Y,S,A
 ;BGPDEPS=DATE SCREEN^SCREEN ITEM^SCREEN RESULT^FU PLAN ITEM
 S BUDVS="BUDVS"
 D ALLV^APCLAPIU(P,BDATE,EDATE,.BUDVS)
 S BUDX=0,BUDD=0
 F  S BUDX=$O(BUDVS(BUDX)) Q:BUDX'=+BUDX  D
 .S BUDV=$P(BUDVS(BUDX),U,5)
 .;CPT
 .S BUDY=0  F  S BUDY=$O(^AUPNVCPT("AD",BUDV,BUDY)) Q:BUDY'=+BUDY  D
 ..Q:'$D(^AUPNVCPT(BUDY,0))
 ..Q:'$$ICD^ATXCHK($P(^AUPNVCPT(BUDY,0),U),$O(^ATXAX("B","BUD DEPRESSION SCREEN CPTS",0)),1)
 ..S BUDDEPS($$VD^APCLV(V))=$$VD^APCLV(BUDV)_U_"CPT: "_$$GET1^DIQ(9000010.18,BUDY,.01)_U_"NEG"
 .;====================================
BHNORES ;
 S D=0,E=9999999-BDATE,D=9999999-EDATE-1_".99" F  S D=$O(^AMHREC("AE",P,D)) Q:D'=+D!($P(D,".")>E)  S V=0 F  S V=$O(^AMHREC("AE",P,D,V)) Q:V'=+V  D
 .S X=0 F  S X=$O(^AMHRPROC("AD",V,X)) Q:X'=+X  S BUDP=$P($G(^AMHRPROC(X,0)),U) D
 ..Q:'BUDP
 ..Q:'$$ICD^ATXCHK(BUDP,$O(^ATXAX("B","BUD DEPRESSION SCREEN CPTS",0)),1)
 ..S BUDDEPS((9999999-$P(D,".")))=9999999-$P(D,".")_U_"BH CPT: "_$$GET1^DIQ(9002011.04,X,.01)_U_"NEG"
 S Y=$O(BUDDEPS(0)) I Y Q BUDDEPS(Y)
 Q ""
PHQ(Y) ;
 I Y="PHQ2" Q 1
 I Y="PHQ9" Q 1
 I Y="PHQT" Q 1
 Q ""
MRES(T,R) ;
 I T="PHQ9",R'<10 Q "POS "_R
 I T="PHQ2",R'<3 Q "POS "_R
 I T="PHQT",R'<10 Q "POS "_R
 Q "NEG "_R
