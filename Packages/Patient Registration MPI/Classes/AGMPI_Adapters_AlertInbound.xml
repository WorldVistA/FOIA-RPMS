<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux for x86-64) 2014.1.3 (Build 775)" ts="2018-09-27 12:54:34">
<Class name="AGMPI.Adapters.AlertInbound">
<Super>Ens.InboundAdapter</Super>
<TimeChanged>64204,48429.622735</TimeChanged>
<TimeCreated>64204,48429.622735</TimeCreated>

<Method name="OnTask">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set tSC=$$$OK
	
	do {
		&sql(SELECT COUNT(*) INTO :numalerts FROM AGMPI_Alerts.AlertItem WHERE ReportedTimestamp IS NULL)
		if 'numalerts { quit }  // Go to sleep for the duration of the polling period.

		set req=##class(Ens.AlertRequest).%New()
		
		// If just one alert, report it.
		if numalerts=1 {
			&sql(SELECT ID INTO :id FROM AGMPI_Alerts.AlertItem WHERE ReportedTimestamp IS NULL ORDER BY CreatedTimestamp)
			set alertobj=##class(AGMPI.Alerts.AlertItem).%OpenId(id)
			if $IsObject(alertobj) {
				set req.SourceConfigName=alertobj.SourceConfigName
				set req.AlertText=alertobj.AlertText
				set alertobj.ReportedTimestamp=$zdt($h,3,1)
				do alertobj.%Save()
			}
			else {
				set req.SourceConfigName=..BusinessHost.%ConfigName
				set req.AlertText="Unable to open alert object "_id
			}
			set tSC=..BusinessHost.ProcessInput(req)
			quit
		}
		
		// If more than one alert, send a digest.
		set nl=$c(13,10)
		kill alerts
		
		&sql(DECLARE C1 CURSOR FOR SELECT ID, Category INTO :id, :cat FROM AGMPI_Alerts.AlertItem WHERE ReportedTimestamp IS NULL ORDER BY CreatedTimestamp)
		&sql(OPEN C1)
		&sql(FETCH C1)
		
		while 'SQLCODE {
			if ((id="") || ('##class(AGMPI.Alerts.AlertItem).%ExistsId(id))) {
				&sql(FETCH C1)
				continue
			}
			
			if cat="" { set cat="Unknown" }
			
			set alertobj=##class(AGMPI.Alerts.AlertItem).%OpenId(id)
			set alertobj.ReportedTimestamp=$zdt($h,3,1)
			do alertobj.%Save()
			
			set alerts(0)=$g(alerts(0))+1
			
			if +$g(alerts(cat))=0 {
				set alerts(cat)=1
				set alerts(cat,0)=alertobj.AlertText
			}
			else {
				set alerts(cat)=$g(alerts(cat))+1
			}
			
			&sql(FETCH C1)
		}
		
		&sql(CLOSE C1)

		set req.SourceConfigName="Multiple alerts"
		set req.AlertText=alerts(0)_$s(alerts(0)=1:" alert",1:" alerts")_" reported"_nl_nl
		
		set cat=0
		for {
			set cat=$o(alerts(cat))
			quit:cat=""
			
			set req.AlertText=req.AlertText_"Category '"_cat_"': "_alerts(cat)_$s(alerts(cat)=1:" alert",1:" alerts")_nl
			if alerts(cat)>1 { set req.AlertText=req.AlertText_"Sample alert: " }
			set req.AlertText=req.AlertText_alerts(cat,0)_nl_nl
		}

		set tSC=..BusinessHost.ProcessInput(req)
		
	} while 0
	
	quit tSC
]]></Implementation>
</Method>
</Class>
</Export>
